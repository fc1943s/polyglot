open rust_operators

union menu_tab =
    | History
    | Settings

inl render () : _ leptos.div =
    leptos.log $'"content.render ()"'

    inl _database = state.use_database ()

    inl location = leptos.use_location ()

    inl url : string =
        inl url_pathname = location |> leptos.location_pathname |> leptos.memo_get |> sm'.from_std_string
        inl url_search = location |> leptos.location_search |> leptos.memo_get |> sm'.from_std_string
        inl url_search =
            if url_search = ""
            then ""
            else $'$"?{!url_search}"'
        $"!url_pathname + !url_search"

    let (tab, set_tab) = History |> leptos.create_signal
    let (url_hash, set_url_hash) = url |> leptos.create_signal

    leptos.create_effect fun () =>
        inl hash = location |> leptos.location_hash |> leptos.memo_get
        inl new_url_hash = $'$"{!url}{!hash}"'
        leptos.log $'"content.render () / effect / new_url_hash: " + !new_url_hash + ""'
        new_url_hash |> leptos.signal_set set_url_hash

    leptos.create_effect fun () =>
        inl hash = location |> leptos.location_hash |> leptos.memo_get
        inl hash = hash |> sm'.strip_prefix '#' |> optionm'.unwrap_or #""
        inl hash_url = hash |> leptos.url_try_from

        inl hash_url = hash_url |> resultm.unwrap

        inl hash_url_log = hash_url |> sm'.format_pretty
        leptos.log $'"content.render () / effect / hash_url: " + string !hash_url_log + ""'

        inl hash_pathname = hash_url |> leptos.url_pathname
        inl tab =
            if hash_pathname |> sm'.starts_with ("/settings" |> sm'.to_std_string)
            then Settings
            else History
        inl tab_log = tab |> sm'.format_pretty

        leptos.log $'"content.render () / effect / new_tab: " + string !tab_log + ""'

        tab |> leptos.signal_set set_tab

    leptos.div [
        $'"class=\\\"bg-gray-50 flex flex-1 flex-col items-stretch min-h-screen text-gray-700 text-sm\\\""'
    ] fun () =>
        ;[
            rust.move (fun () => tab) fun tab =>
                match tab |> leptos.signal_get with
                | History => "" |> leptos.text_view
                | Settings =>
                    ;[
                        leptos.accordion "View" fun () =>
                            leptos.view_map_grid fun () =>
                                leptos.grid_pair
                                    { class = $'"items-center"' }
                                    ("Dark Mode" |> leptos.text_view |> leptos.view_to_fragment)
                                    (dark_mode_toggle.render () |> leptos.element_to_fragment)
                                |> leptos.element_to_fragment
                        |> leptos.element_to_view

                        leptos.accordion "Connection" fun () =>
                            leptos.view_map_grid fun () =>
                                leptos.grid_pair
                                    { class = $'"items-baseline"' }
                                    ("Explorer Backend Host" |> leptos.text_view |> leptos.view_to_fragment)
                                    (explorer_backend_host_input.render () |> leptos.element_to_fragment)
                                |> leptos.element_to_fragment
                        |> leptos.element_to_view
                    ]
                    |> leptos.views_to_view
                | _ => ;[] |> leptos.views_to_view
            |> leptos.closure_to_view

            leptos.div [
                $'"class=\\\"flex flex-1\\\""'
            ] fun () =>
                leptos.div [
                    $'"class=\\\"flex flex-1 [align-items:flex-end]\\\""'
                ] fun () =>
                    ;[
                        leptos.div [
                            $'"class=\\\"hidden\\\""'
                        ] fun () =>
                            ;[
                                leptos.label [
                                    $'"for=\\\"Tab\\\""'
                                    $'"class=\\\"sr-only\\\""'
                                ] fun () =>
                                    "Tab" |> leptos.text_fragment
                                |> leptos.element_to_view

                                leptos.select [
                                    $'"id=\\\"Tab\\\""'
                                    $'"class=\\\"w-full rounded-md border-gray-200\\\""'
                                ] fun () =>
                                    ;[
                                        leptos.option'
                                            (fun () => (tab |> leptos.signal_get) = History)
                                            "History"
                                            |> leptos.element_to_view

                                        leptos.option'
                                            (fun () => (tab |> leptos.signal_get) = Settings)
                                            "Settings"
                                            |> leptos.element_to_view
                                    ]
                                    |> leptos.views_to_fragment
                                |> leptos.element_to_view
                            ]
                            |> leptos.views_to_fragment
                        |> leptos.element_to_view

                        leptos.div [
                            $'"class=\\\"flex flex-1 [overflow-x:auto] [overflow-y:hidden]\\\""'
                        ] fun () =>
                            leptos.div [
                                $'"class=\\\"flex flex-1 border-t border-gray-200\\\""'
                            ] fun () =>
                                leptos.nav [
                                    $'"class=\\\"-mb-px flex [flex-flow:wrap] [flex-grow:1] [flex-shrink:0] gap-[3px] [overflow-wrap:anywhere]\\\""'
                                    $'"aria-label=\\\"Tabs\\\""'
                                ] fun () =>
                                    ;[
                                        leptos.tab_link
                                            $'!url + "#/history"'
                                            (leptos.history_svg ())
                                            "History"
                                            (fun () => (tab |> leptos.signal_get) = History)
                                        |> leptos.element_to_view

                                        leptos.tab_link
                                            $'!url + "#/settings"'
                                            (leptos.settings_svg ())
                                            "Settings"
                                            (fun () => (tab |> leptos.signal_get) = Settings)
                                        |> leptos.element_to_view
                                    ]
                                    |> leptos.views_to_fragment
                                |> leptos.element_to_fragment
                            |> leptos.element_to_fragment
                        |> leptos.element_to_view
                    ]
                    |> leptos.views_to_fragment
                |> leptos.element_to_fragment

            |> leptos.element_to_view
        ]
        |> leptos.views_to_fragment
