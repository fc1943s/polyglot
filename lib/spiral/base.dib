#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# base

#!spiral

//// test

open testing

#!markdown

## array

#!spiral

inl array x = x

#!markdown

## prototype

#!spiral

prototype append t : t -> t -> t
prototype of_string t : string -> t
prototype to_string t : t -> string

#!markdown

## not'

#!spiral

inl not' x =
    join not x

#!markdown

## (+.)

#!spiral

inl (+.) forall t. (a : t) (b : t) : t =
    $"!a + !b"

#!markdown

## (-.)

#!spiral

inl (-.) forall t. (a : t) (b : t) : t =
    $"!a - !b"

#!markdown

## (*.)

#!spiral

inl (*.) forall t. (a : t) (b : t) : t =
    $"!a * !b"

#!markdown

## (/.)

#!spiral

inl (/.) forall t. (a : t) (b : t) : t =
    $"!a / !b"

#!markdown

## (=.)

#!spiral

inl (=.) forall t. (a : t) (b : t) : bool =
    $"!a = !b"

#!markdown

## (<>.)

#!spiral

inl (<>.) forall t. (a : t) (b : t) : bool =
    $"!a <> !b"

#!markdown

## (||>)

#!spiral

inl (||>) (arg1, arg2) fn =
    arg2 |> fn arg1

#!markdown

## (/@)

#!spiral

inl (/@) a b =
    b |> append a

#!markdown

## target_runtime

#!spiral

union target_runtime =
    | Native
    | Wasm
    | Contract

#!markdown

## target

#!spiral

union target =
    | Rust : target_runtime
    | Fsharp : target_runtime
    | TypeScript : target_runtime
    | Python : target_runtime

#!markdown

## defaultof

#!spiral

inl defaultof forall t. () : t =
    $'Unchecked.defaultof<`t>'

#!markdown

## emit

#!spiral

inl emit forall t. (x : t) : t =
    $'!x '

#!markdown

## unbox

#!spiral

inl unbox forall t u. (x : t) : u =
    $"!x |> unbox<`u>"

#!markdown

## run_target

#!spiral

inl run_target forall t. (fn : target -> (() -> t)) : t =
    inl close x =
        $'!x\n#endif'
    inl result : optionm'.option' t = optionm'.none' ()
    ($"true in let mutable _!result = !result" : bool) |> ignore
    $"\n#if FABLE_COMPILER || WASM || CONTRACT"
    $"\n#if FABLE_COMPILER_RUST && \!WASM && \!CONTRACT"
    fn (Rust Native) () |> close
    $"\n#if FABLE_COMPILER_RUST && WASM"
    fn (Rust Wasm) () |> close
    $"\n#if FABLE_COMPILER_RUST && CONTRACT"
    fn (Rust Contract) () |> close
    $"\n#if FABLE_COMPILER_TYPESCRIPT"
    fn (TypeScript Native) () |> close
    $"\n#if FABLE_COMPILER_PYTHON"
    fn (Python Native) () |> close
    $"\n#else"
    fn (Fsharp Native) () |> close
    $"|> fun x -> _!result <- Some x"
    $'_!result.Value'

#!spiral

//// test

run_target function
    | Fsharp (Native) => fun () => $"1uy"
    | _ => fun () => $"2uy"
|> _assert_eq 1u8

#!spiral

//// test
///! rust

run_target function
    | Rust (Native) => fun () => $"1uy"
    | _ => fun () => $"2uy"
|> _assert_eq 1u8

#!markdown

## exn

#!spiral

nominal exn = $"exn"

#!markdown

## try

#!spiral

inl try forall t. (fn : () -> t) (ex_fn : exn -> option t) : option t =
    inl none = None : option t
    inl some (s : t) = Some s
    $'try !fn () |> !some with ex -> !ex_fn ex '

#!markdown

## try_unit

#!spiral

inl try_unit forall t. (fn : () -> ()) (ex_fn : exn -> ()) : t =
    $"try"
    fn ()
    |> ignore
    $"with ex ->"
    ex_fn $'ex'
    |> ignore
    $'(*'
    $'*)'

#!markdown

## try_finally

#!spiral

inl try_finally forall t. (fn : () -> ()) (finally : () -> ()) : t =
    $"try"
    fn ()
    |> ignore
    $"finally"
    finally ()
    |> ignore
    $'(*'
    $'*)'

#!markdown

## fix_condition

#!spiral

inl fix_condition x a b =
    if x ()
    then a () |> fun x => $'(*' : ()
    else
        $'*) else' : ()
        b () |> fun x => $'(*' : ()
    |> fun x => $'*)' : ()

#!markdown

## any

#!spiral

nominal any = $"obj"

#!markdown

## i32

#!spiral

inl i32 forall t. (x : t) : i32 =
    $"int32 !x"

#!markdown

## i64

#!spiral

inl i64 forall t. (x : t) : i64 =
    $"int64 !x"

#!markdown

## f32

#!spiral

inl f32 forall t. (x : t) : f32 =
    $"float32 !x"

#!markdown

## f64

#!spiral

inl f64 forall t. (x : t) : f64 =
    $"float !x"

#!markdown

## unativeint

#!spiral

nominal unativeint = $"unativeint"

inl unativeint forall t {int}. (n : t) : unativeint =
    $"unativeint !n"

#!markdown

## convert_i32

#!spiral

inl convert_i32 forall t. (x : t) : i32 =
    x |> $'System.Convert.ToInt32'

#!markdown

## pair

#!spiral

type pair a b = $"(`a * `b)"

inl pair x y =
    x, y

#!markdown

## null

#!spiral

inl null forall t. () : t =
    $"null |> unbox<`t>"

#!markdown

## get_entry_assembly_name

#!spiral

inl get_entry_assembly_name () : string =
    run_target function
        | Fsharp (Native) => fun () =>
            $'System.Reflection.Assembly.GetEntryAssembly().GetName().Name'
        | Rust (Native) => fun () =>
            open rust_operators
            !\($'"env\!(\\"CARGO_PKG_NAME\\").into()"')
        | target => fun () => null ()

#!markdown

## use

#!spiral

inl use forall t. (x : t) : t =
    $"use !x = !x " : ()
    $'!x '

#!markdown

## get_union_fields

#!spiral

inl get_union_fields forall union_type. () : list union_type =
    real
        real_core.union_to_record
            `union_type
            forall union_record_type. =>
                real_core.record_type_fold
                    fun acc key =>
                        forall value. =>
                            inl item = real_core.nominal_create `union_type (key, ())
                            (::) `union_type item acc
                    (Nil `union_type)
                    `union_record_type

#!spiral

//// test

inl fields : list target_runtime = get_union_fields ()
fields
|> listm'.box
|> listm'.to_array'
|> am'.sort
|> fun x => x : a i32 _
|> _assert_eq (a ;[ Native; Wasm; Contract ])

#!markdown

## get_union_fields_untag

#!spiral

inl get_union_fields_untag forall union_type. () : list union_type =
    real
        real_core.union_to_record
            `union_type
            forall union_record_type. =>
                inl result =
                    real_core.record_type_fold_back
                        fun _key =>
                            forall value. (acc, (i : i32)) =>
                                inl item : union_type =
                                    real_core.union_untag `union_type i
                                        (fun k => forall v. => real_core.nominal_create `union_type (k, ()))
                                        (fun _ => failwith `union_type "Invalid tag.")
                                (::) `union_type item acc, (+) `i32 i 1
                        `union_record_type
                        (Nil `union_type, 0i32)
                inl result = fst `(list union_type) `i32 result
                listm.rev `union_type result

#!spiral

//// test

inl fields : list target_runtime = get_union_fields_untag ()
fields
|> listm'.box
|> listm'.to_array'
|> fun x => x : a i32 _
|> _assert_eq (a ;[ Native; Wasm; Contract ])
