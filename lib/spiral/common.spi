// // # common

// // ## append

prototype append t : t -> t -> t

// // ## pair

inl pair x y =
    x, y

// // ## ||>

inl (||>) (arg1, arg2) fn = arg2 |> fn arg1

// // ## flip

inl flip fn a b =
    fn b a

// // ## join_body

inl join_body body acc x =
    if var_is x |> not
    then body acc x
    else
        inl acc = dyn acc
        join body acc x

// // ## join_body_unit

inl join_body_unit body d x =
    if var_is d |> not
    then body x
    else
        inl x = dyn x
        join body x

// // ## run_target

union target_runtime =
    | Native
    | Wasm

union target =
    | Rust : target_runtime
    | Fsharp

inl run_target forall t. (fn : target -> (() -> t)) : t =
    $"let mutable result = None"
    $"#if FABLE_COMPILER_RUST"
    $"#if \!WASM"
    fn (Rust Native) () |> fun x => $"!x"
    $"#else"
    fn (Rust Wasm) () |> fun x => $"!x"
    $"#endif"
    $"#else"
    fn Fsharp () |> fun x => $"!x"
    $"#endif"
    $"|> fun x -> result <- Some x"
    $"result |> Option.get"

// // ## format_debug

inl format_debug x =
    $"$\"%A{!x}\"" : string

// // ## nameof

inl nameof x : string =
    $"nameof !x"

// // ## obj_to_string

inl obj_to_string x : string =
    $"!x.ToString ()"

// // ## get_environment_variable

inl get_environment_variable (var : string) : string =
    $"System.Environment.GetEnvironmentVariable !var"
