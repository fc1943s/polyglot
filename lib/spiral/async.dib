#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# async

#!spiral

// // test

open testing

#!spiral

open rust_operators

#!markdown

## types

#!spiral

inl types () =
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"std::future::Future<Output = $0>\")>] type Future<'T> = class end"
    global "[<Fable.Core.Erase; Fable.Core.Emit(\"futures::future::TryJoinAll<$0>\")>] type TryJoinAll<'T> = class end"

#!markdown

## async

#!spiral

nominal async t = $"Async<`t>"

#!markdown

## future

#!spiral

nominal future t = $"Future<`t>"

#!markdown

## task

#!spiral

nominal task t =
    `(
        typecase t with
        | () => $'' : $"System.Threading.Tasks.Task"
        | _ => $'' : $"System.Threading.Tasks.Task<`t>"
    )

#!markdown

## future_pin

#!spiral

type future_pin t = rust.pin (rust.box (rust.dyn' (future t)))

#!markdown

## try_join_all

#!spiral

nominal try_join_all t = $"TryJoinAll<`t>"

#!markdown

## future_init

#!spiral

inl future_init forall t. (depth : (u8 * u8)) (flag : u8) (x : () -> t) : future_pin t =
    join
        if flag = 1
        then !\($'"let __result = Box::pin(async move { //"')
        else !\($'"let __result = Box::pin(async { //"')

        let x' = x ()
        inl x' = join x'

        x' |> rust.fix_closure depth

        !\($'"__result"')

#!markdown

## try_join_all

#!spiral

inl try_join_all forall t. (x : am'.vec (future_pin (resultm.result' t sm'.std_string))) : try_join_all (future_pin (resultm.result' t sm'.std_string)) =
    inl x = join x
    !\($'"futures::future::try_join_all(!x)"')

#!markdown

## await'

#!spiral

inl await' forall t. (x : try_join_all (future_pin (resultm.result' t sm'.std_string))) : resultm.result' (am'.vec t) sm'.std_string =
    !\($'"!x.await"')

#!markdown

## await

#!spiral

inl await forall t. (x : future_pin t) : t =
    !\($'"!x.await"')

#!markdown

## new_async_unit

#!spiral

inl new_async_unit forall t. (fn : () -> ()) : async t =
    run_target function
        | Fsharp (Native) => fun () =>
            inl result : optionm'.option' (async t) = optionm'.none' ()
            $"let mutable _!result = !result"
            $"async {"
            fn ()
            $"}"
            $"|> fun x -> _!result <- Some x"
            $"_!result |> Option.get"
        | _ => fun () => null ()

#!markdown

## new_async

#!spiral

inl new_async forall t. (fn : () -> t) : async t =
    new_async_unit (fn >> ignore)

#!markdown

## new_task

#!spiral

inl new_task forall t. (fn : () -> t) : task t =
    run_target function
        | Fsharp (Native) => fun () =>
            inl result : optionm'.option' (task t) = optionm'.none' ()
            $"let mutable _!result = !result"
            $"task {"
            fn () |> ignore
            $"}"
            $"|> fun x -> _!result <- Some x"
            $"_!result |> Option.get"
        | _ => fun () => null ()

#!markdown

## await_task

#!spiral

inl await_task forall t. (a : task t) : async t =
    run_target function
        | Fsharp (Native) => fun () =>
            a |> $'Async.AwaitTask'
        | _ => fun () => null ()

#!markdown

## ignore

#!spiral

inl ignore forall t. (a : async t) : async () =
    run_target function
        | Fsharp (Native) => fun () =>
            a |> $'Async.Ignore'
        | _ => fun () => null ()

#!markdown

## run_synchronously

#!spiral

inl run_synchronously forall t. (a : async t) : t =
    run_target function
        | Fsharp (Native) => fun () =>
            a |> $'Async.RunSynchronously'
        | _ => fun () => null ()

#!markdown

## start_child

#!spiral

inl start_child forall t. (a : async t) : async (async t) =
    run_target function
        | Fsharp (Native) => fun () =>
            a |> $'Async.StartChild'
        | _ => fun () => null ()

#!markdown

## start_child_timeout

#!spiral

inl start_child_timeout forall t. (timeout : i32) (a : async t) : async (async t) =
    run_target function
        | Fsharp (Native) => fun () =>
            $'Async.StartChild (!a, !timeout)'
        | _ => fun () => null ()

#!markdown

## sleep

#!spiral

inl sleep (ms : i32) : async () =
    run_target function
        | Fsharp (Native) => fun () =>
            ms |> $'Async.Sleep'
        | _ => fun () => null ()

#!markdown

## do

#!spiral

inl do (a : async ()) : () =
    $'do\! !a '

#!markdown

## let'

#!spiral

inl let' forall t. (a : async t) : t =
    $'let\! !a = !a '
    $'!a '

#!markdown

## return_await

#!spiral

inl return_await forall t. (a : async t) : () =
    $'return\! !a '

#!markdown

## return_await'

#!spiral

inl return_await' forall t. (a : async t) : t =
    $'return\! !a '

#!markdown

## map

#!spiral

inl map forall t u. (fn : t -> u) (a : async t) : async u =
    fun () =>
        inl x = a |> let'
        fn x |> return
    |> new_async_unit

#!markdown

## choice

#!spiral

union choice t1 t2 =
    | Choice1Of2 : t1
    | Choice2Of2 : t2

nominal choice' t1 t2 = $'Choice<`t1, `t2>'

#!markdown

## choice_unbox

#!spiral

inl choice_unbox forall t1 t2. (choice : choice' t1 t2) : choice t1 t2 =
    run_target function
        | Fsharp (Native) => fun () =>
            inl choice1Of2 (x : t1) : _ _ t2 = Choice1Of2 x
            inl choice2Of2 (x : t2) : _ t1 _ = Choice2Of2 x
            $'match !choice with Choice1Of2 x -> !choice1Of2 x | Choice2Of2 x -> !choice2Of2 x'
        | _ => fun () => null ()

#!markdown

## catch'

#!spiral

inl catch' forall t e. (a : async t) : async (choice' t e) =
    run_target function
        | Fsharp (Native) => fun () =>
            a |> $'Async.Catch'
        | _ => fun () => null ()

#!markdown

## catch

#!spiral

inl catch forall t e. (a : async t) : async (result t e) =
    a
    |> catch'
    |> map choice_unbox
    |> map function
        | Choice1Of2 result => Ok result
        | Choice2Of2 ex => Error ex

#!markdown

## run_with_timeout_async

#!spiral

inl run_with_timeout_async forall t. (timeout : i32) (fn : async t) : async (option t) =
    run_target function
        | Fsharp (Native) => fun () =>
            fun () =>
                inl get_locals () = $'$"timeout: {!timeout} / {!get_locals ()}"' : string
                inl child = fn |> start_child_timeout timeout |> let'
                child
                |> catch
                |> map function
                    | Ok result => Some result
                    | Error ex when ex |> sm'.format_debug |> sm'.contains "System.TimeoutException" =>
                        trace Debug (fun () => $'"run_with_timeout_async"' : string) get_locals
                        None
                    | Error (ex : exn) =>
                        inl ex = ex |> sm'.format_exception
                        trace Critical (fun () => $'$"run_with_timeout_async** / ex: {!ex}"' : string) get_locals
                        None
                |> return_await
                // $'(*'
                // $'*)'
            |> new_async_unit
        | _ => fun () => null ()

#!markdown

## run_with_timeout

#!spiral

inl run_with_timeout timeout fn =
    fn
    |> run_with_timeout_async timeout
    |> run_synchronously

#!markdown

## cancellation_token

#!spiral

nominal cancellation_token = $'System.Threading.CancellationToken'
nominal cancellation_token_source = $'System.Threading.CancellationTokenSource'

inl cancellation_token () : async cancellation_token =
    $'Async.CancellationToken'

#!spiral

inl default_cancellation_token () : cancellation_token =
    $'Async.DefaultCancellationToken'

#!spiral

inl cancellation_source_token (x : cancellation_token_source) : cancellation_token =
    $'!x.Token'

#!spiral

inl create_linked_token_source (x : array_base cancellation_token) : cancellation_token_source =
    x |> $'System.Threading.CancellationTokenSource.CreateLinkedTokenSource'

#!markdown

## merge_cancellation_token_with_default_async

#!spiral

inl merge_cancellation_token_with_default_async (token : cancellation_token) : async cancellation_token =
    run_target function
        | Fsharp (Native) => fun () =>
            fun () =>
                inl ct = cancellation_token () |> let'
                inl dct = default_cancellation_token ()
                inl cts = create_linked_token_source ;[ ct; dct; token ]
                cts |> cancellation_source_token |> return
            |> new_async_unit
        | _ => fun () => null ()

#!markdown

## main

#!spiral

inl main () =
    $"let merge_cancellation_token_with_default_async x = !merge_cancellation_token_with_default_async x" : ()
