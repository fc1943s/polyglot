// // # listm

// // ## /@

inl (/@) a b = b |> listm.append a

inl init_series start end inc =
    inl total : f64 = conv ((end - start) / inc) + 1
    listm.init total (conv >> (*) inc >> (+) start)

// // ## try_item

inl rec try_item i = function
    | Cons (x, _) when i = 0 => Some x
    | Cons (_, xs) => try_item (i - 1) xs
    | Nil => None

// // ## list_item

inl item i =
    try_item i >> optionm.value

// // ## try_item_

let rec try_item_ i = function
    | Cons (x, _) when i = 0 => Some x
    | Cons (_, xs) => try_item_ (i - 1) xs
    | Nil => None

// // ## item_

inl item_ i =
    try_item_ i >> optionm.value

// // ## lazy_list

union rec lazy_list t =
    | LazyCons : t * (() -> lazy_list t)
    | LazyNil

inl rec lazy_list list =
    match list with
    | [] => LazyNil
    | x :: xs => LazyCons (x, fun () => lazy_list xs)

inl rec try_lazy_item i = function
    | LazyCons (x, _) when i = 0 => Some x
    | LazyCons (_, fn) => try_lazy_item (i - 1) (fn ())
    | LazyNil => None

inl lazy_item i =
    try_lazy_item i >> optionm.value

inl try_lazy_item_ i list =
    inl i = mut i
    inl list = mut list
    let rec loop () =
        match *list with
        | LazyCons (x, _) when *i = 0 => Some x
        | LazyCons (_, fn) =>
            i <- *i - 1
            list <- fn ()
            loop ()
        | LazyNil => None
    loop ()

inl lazy_item_ i =
    try_lazy_item_ i >> optionm.value

let rec try_lazy_item__ i list =
    inl i = mut i
    inl list = mut list
    inl result = mut None
    let rec loop () =
        match *list with
        | LazyCons (x, _) when *i = 0 => result <- Some x
        | LazyCons (_, fn) =>
            i <- *i - 1
            list <- fn ()
            loop ()
        | LazyNil => ()
    loop ()
    *result

inl lazy_item__ i =
    try_lazy_item__ i >> optionm.value

// // ## new_infinite_lazy_list

inl new_infinite_lazy_list fn =
    inl rec loop n =
        LazyCons (fn n, fun () => loop (n + 1))
    loop 0

inl new_infinite_lazy_list_ fn =
    let rec loop n =
        LazyCons (fn n, fun () => loop (n + 1))
    loop 0

inl new_finite_lazy_list fn max =
    inl rec loop n =
        if n >= max
        then LazyNil
        else LazyCons (fn n, fun () => loop (n + 1))
    loop 0

inl new_finite_lazy_list_ fn max =
    let rec loop n =
        if n >= max
        then LazyNil
        else LazyCons (fn n, fun () => loop (n + 1))
    loop 0

// // ## memoize_lazy_list

union memoized_lazy_list t =
    | NotComputed : () -> lazy_list t
    | Computed : lazy_list t

inl memoize_lazy_list list =
    inl rec loop list =
        inl state = mut (NotComputed list)
        fun () =>
            match *state with
            | Computed x => x
            | NotComputed fn =>
                inl new_state =
                    match fn () with
                    | LazyNil => LazyNil
                    | LazyCons (x, fn) => LazyCons (x, loop fn)
                state <- Computed new_state
                new_state
    loop (fun () => list)

inl memoize_lazy_list_ list =
    let rec loop list =
        inl state = mut (NotComputed list)
        fun () =>
            match *state with
            | Computed x => x
            | NotComputed fn =>
                inl new_state =
                    match fn () with
                    | LazyNil => LazyNil
                    | LazyCons (x, fn) => LazyCons (x, loop fn)
                state <- Computed new_state
                new_state
    loop (fun () => list)

// // ## sum

inl sum list =
    list |> listm.fold (+) 0

// // ## unzip

inl unzip list =
    list
    |> listm.fold
        (fun (acc_x, acc_y) (x, y) =>
            x :: acc_x, y :: acc_y
        )
        ([], [])
    |> fun x, y =>
        x |> listm.rev, y |> listm.rev

inl unzip_ list =
    let rec loop acc_x acc_y = function
        | Cons ((x, y), xs) => loop (x :: acc_x) (y :: acc_y) xs
        | Nil => acc_x |> listm.rev, acc_y |> listm.rev
    loop [] [] list

// // ## try_index_of

inl try_index_of item list =
    inl rec loop i = function
        | [] => None
        | x :: xs =>
            if x = item
            then Some i
            else loop (i + 1) xs
    loop 0 list

inl index_of item =
    try_index_of item >> optionm.value

inl try_index_of_ item list =
    let rec loop i = function
        | [] => None
        | x :: xs =>
            if x = item
            then Some i
            else loop (i + 1) xs
    loop 0 list

inl index_of_ item =
    try_index_of_ item >> optionm.value

inl try_index_of__ item list =
    inl i = mut 0
    inl list = mut list
    inl result = mut None
    let rec loop () =
        match *list with
        | [] => result <- None
        | x :: xs =>
            if x = item
            then result <- Some *i
            else
                i <- *i + 1
                list <- xs
                loop ()
    loop ()
    *result

inl index_of__ item =
    try_index_of__ item >> optionm.value

// // ## try_find

inl try_find fn list =
    inl rec loop = function
        | [] => None
        | x :: xs =>
            if fn x
            then Some x
            else loop xs
    loop list

inl try_find_ fn list =
    let rec loop = function
        | [] => None
        | x :: xs =>
            if fn x
            then Some x
            else loop xs
    loop list

inl find x =
    try_find x >> optionm.value

inl find_ x =
    try_find_ x >> optionm.value

// // ## choose

inl choose f l =
    (l, [])
    ||> listm.foldBack fun x acc =>
        match f x with
        | Some y => y :: acc
        | None => acc

inl choose_ f l =
    let rec loop acc = function
        | [] => acc |> listm.rev
        | x :: xs =>
            match f x with
            | Some y => loop (y :: acc) xs
            | None => loop acc xs
    loop [] l

// // ## zip_with

inl zip_with fn xs ys =
    inl rec loop acc xs ys =
        match xs, ys with
        | Cons (x, xs), Cons (y, ys) =>
            loop (fn x y :: acc) xs ys
        | _ => listm.rev acc
    loop [] xs ys

inl zip_with_ fn xs ys =
    let rec loop acc xs ys =
        match xs, ys with
        | Cons (x, xs), Cons (y, ys) =>
            loop (fn x y :: acc) xs ys
        | _ => listm.rev acc
    loop [] xs ys

// // ## zip

inl zip xs ys =
    zip_with pair xs ys

inl zip_ xs ys =
    zip_with_ pair xs ys

// // ## indexed

inl indexed list =
    list
    |> listm.fold
        (fun (acc, i) x =>
            (i, x) :: acc, i + 1
        )
        ([], 0)
    |> fst
    |> listm.rev
