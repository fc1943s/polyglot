#!meta

{"kernelInfo":{"defaultKernelName":"spiral","items":[]}}

#!markdown

# crypto

#!spiral

open rust_operators

#!spiral

//// test

open testing
open file_system_operators

#!markdown

## types

#!spiral

inl types () =
    global "#if FABLE_COMPILER\n[<Fable.Core.Erase; Fable.Core.Emit(\"sha2::Sha256\")>]\n#endif\ntype sha2_Sha256 = class end"

#!spiral

inl types () =
    sm'.types ()
    am'.types ()
    threading.types ()
    rust.types ()
    date_time.types ()
    file_system.types ()
    stream.types ()
    runtime.types ()
    types ()

#!markdown

## sha256

#!spiral

nominal sha256 = $'System.Security.Cryptography.SHA256'

inl sha256 () : sha256 =
    $'`sha256.Create' ()

#!markdown

## sha256_compute_hash

#!spiral

inl sha256_compute_hash (x : sha256) (data : a i32 u8) : a i32 u8 =
    data |> $'!x.ComputeHash'

#!markdown

## create_hash

#!spiral

inl create_hash (x : string) : any =
    open ts_operators
    global "type ICryptoCreateHash = abstract createHash: x: string -> obj"
    inl crypto : $'ICryptoCreateHash' = ts.import_all "crypto"
    !\\(x, $'"!crypto.createHash($0)"')

#!markdown

## hash_update

#!spiral

inl hash_update (s : string) (x : any) : any =
    open ts_operators
    !\\((x, s), $'"$0.update($1, \'utf8\')"')

#!markdown

## hash_digest

#!spiral

inl hash_digest (s : string) (x : any) : string =
    open ts_operators
    !\\((x, s), $'"$0.digest($1)"')

#!markdown

## hash_text

#!spiral

inl hash_text (input : string) =
    run_target function
        | Fsharp (Native) => fun () =>
            inl input = join input
            inl sha256 = sha256 () |> use
            input
            |> sm'.utf8_get_bytes
            |> sha256_compute_hash sha256
            |> am.map (sm'.byte_to_string "x2")
            |> seq.of_array'
            |> sm'.concat ""
        | TypeScript _ => fun () =>
            create_hash "sha256"
            |> hash_update input
            |> hash_digest "hex"
        | _ => fun () => null ()

#!spiral

//// test

""
|> hash_text
|> _assert_eq "e3b0c44298fc1c149afbf4c8996fb92427ae41e4649b934ca495991b7852b855"

#!spiral

//// test

" "
|> hash_text
|> _assert_eq "36a9e7f1c95b82ffb99743e0c5c4ce95d83c9a430aac59f84ef3cbfab6145068"

#!markdown

## get_file_hash

#!spiral

inl get_file_hash (path : string) : result string string =
    inl path = path |> file_system.normalize_path
    inl exit_code, result =
        runtime.execution_options fun x => { x with
            command = $'$"pwsh -c \\\"(Get-FileHash \\\\\\"{!path}\\\\\\" -Algorithm SHA256).Hash\\\""'
        }
        |> runtime.execute_with_options
    if exit_code = 0
    then Ok result
    else Error result

#!spiral

//// test
//// print_code=false
///! rust

types ()
inl temp_folder, disposable = file_system.create_temp_directory ()

inl file_name = join "test.txt"
inl path = temp_folder </> file_name
"" |> file_system.write_all_text path
path
|> get_file_hash
|> resultm.get
|> _assert_eq "E3B0C44298FC1C149AFBF4C8996FB92427AE41E4649B934CA495991B7852B855"
disposable |> use |> ignore

#!markdown

## hash_file

#!markdown

## sha256'

#!spiral

nominal sha256' = $'sha2_Sha256'

#!markdown

## new_sha256

#!spiral

inl new_sha256 () : sha256' =
    !\($'"sha2::Sha256::new()"')

#!markdown

## hasher_update

#!spiral

inl hasher_update forall t dim. (slice : am'.slice' t dim) (hasher : sha256') : () =
    !\($'"!hasher"')

#!markdown

## hasher_finalize

#!spiral

inl hasher_finalize (hasher : sha256') : sm'.std_string =
    !\($'"!hasher"')

#!markdown

## hash_file

#!spiral

inl hash_file (path : string) : result string string =
    inl path = path |> file_system.normalize_path
    inl file = path |> file_system.file_open |> resultm.unwrap'
    inl reader = file |> stream.new_buf_reader
    inl hasher = new_sha256 ()
    inl buffer = (0, 1014) |> am'.new_slice

    rust.loop fun () =>
        inl count = reader |> stream.buf_reader_read buffer |> resultm.unwrap'
        if count = 0 then rust.break ()
        hasher |> hasher_update (buffer |> am'.slice_range (am'.Start 0) (am'.End fun _ => count))

    hasher |> hasher_finalize |> sm'.from_std_string |> Ok

#!spiral

//// test
//// print_code=false
///! rust

types ()
inl temp_folder, disposable = file_system.create_temp_directory ()

inl file_name = join "test.txt"
inl path = temp_folder </> file_name
"" |> file_system.write_all_text path
path
|> hash_file
|> resultm.get
|> _assert_eq "E3B0C44298FC1C149AFBF4C8996FB92427AE41E4649B934CA495991B7852B855"
disposable |> use |> ignore

#!markdown

## main

#!spiral

inl main () =
    types ()
    $"let hash_text x = !hash_text x" : ()
